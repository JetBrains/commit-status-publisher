/*
 * Copyright 2000-2024 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.nio.file.Paths
import com.github.jk1.license.render.JsonReportRenderer
import com.github.jk1.license.filter.LicenseBundleNormalizer

plugins {
  id "com.github.jk1.dependency-license-report" version "1.17"
}

initializeWorkspace()

def localRepo = anyParamPath('TC_LOCAL_REPO')
def pluginVersion = anyParam('PluginVersion')

ext {
  teamcityVersion = anyParam('teamcityVersion') ?: 'LOCAL-SNAPSHOT'
}

allprojects {
  apply plugin: 'java'

  group = 'org.jetbrains.teamcity'
  version = pluginVersion

  clean {
    delete "target"
  }
}

subprojects {

  apply plugin: 'java'

  repositories {
    if (localRepo) {
      maven {
        name = "local-teamcity-artifacts"
        url "file:///${localRepo}"
      }
    }
    maven {
      url = 'https://download.jetbrains.com/teamcity-repository'
    }
    maven {
      url = 'https://repo.labs.intellij.net/teamcity'
    }

    maven {
      url = 'https://cache-redirector.jetbrains.com/repo.maven.apache.org/maven2'
    }
    mavenLocal()
  }

  sourceCompatibility = '1.8'
}

task copyDependencies(type: Copy, dependsOn: ':commit-status-publisher-server:jar') {
  project(':commit-status-publisher-server').configurations.getAt("runtime").setCanBeResolved(true)
  println("implementation canBeResolved change to ï¼š" + project(':commit-status-publisher-server').configurations.getAt("runtime").canBeResolved)
  from project(':commit-status-publisher-server').configurations.getAt("runtime")
  include 'jsch*.jar'
  into './build/dependencies'
}


task copyPluginFiles(type: Copy, dependsOn: ':commit-status-publisher-server:jar') {
  from '.'
  include 'teamcity-plugin.xml'
  into './build/plugin-files'
  expand(PluginVersion: pluginVersion)
}

task zipPlugin(type: Zip, dependsOn: ['copyDependencies', 'copyPluginFiles']) {
  destinationDir = new File(rootDir, './target')
  into('server') {
    from project(':commit-status-publisher-server').configurations.runtime.allArtifacts.files
    include '*'
    from './build/dependencies'
    include '*'
  }
  into('kotlin-dsl') {
    from 'kotlin-dsl'
    include 'CommitStatusPublisher.xml'
  }
  from './build/plugin-files'
  include 'teamcity-plugin.xml'

  archiveName = 'commit-status-publisher.zip'
}

build.dependsOn zipPlugin

licenseReport {
  renderers = [new JsonReportRenderer('third-party-libraries.json')]
  excludes = ["org.jetbrains.*", "com.jetbrains.*", ".*jackson-bom*"]
  filters = [new LicenseBundleNormalizer(bundlePath: "${project.rootDir}/license-third-party-normalizer.json", createDefaultTransformationRules: false)]
}

def anyParamPath(String... names) {
  def param = anyParam(names);
  if (param == null || param.isEmpty()) {
    return null
  }
  return (Paths.get(param).isAbsolute()) ?
         Paths.get(param) : getRootDir().toPath().resolve(param)
}

def anyParam(String... names) {
  def param
  try {
    param = names.findResult {
      project.hasProperty(it) ? project.getProperty(it) : System.getProperty(it) ?: System.getenv(it) ?: null
    }
    if (param == null || param.isEmpty()) {
      param = null
    }
  } finally {
    println("AnyParam: $names -> $param")
  }
  return param
}

def initializeWorkspace() {
  if (System.getProperty("idea.active") != null) {
    println "Attempt to configure workspace in IDEA"
    def coreVersionProperties = project.projectDir.toPath().parent.parent.resolve(".version.properties")
    if (coreVersionProperties.toFile().exists()) {
      def p = new Properties().tap {
        it.load(new FileInputStream(coreVersionProperties.toFile()))
      }
      p.forEach { k,v ->
        System.setProperty(k, v);
      }
    }
  }
}